plugins {
	id 'fabric-loom' version '1.7.2'
	id 'babric-loom-extension' version '1.7.4'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "local"
if (project.hasProperty("build_number")) {
	version = "git-" + project.findProperty("build_number")
}

loom {
	mixin {
		defaultRefmapName = "${archivesBaseName}.refmap.json"
	}
}

repositories {
	maven { url = 'https://maven.glass-launcher.net/snapshots' }
	maven { url = 'https://maven.glass-launcher.net/releases' }
	maven { url = 'https://maven.glass-launcher.net/babric' }
	maven { url = 'https://maven.minecraftforge.net/' }
	maven { url = 'https://jitpack.io' }
	maven { url = 'https://maven.nathatpas.tel/releases' }
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
	mavenCentral()
}

dependencies {
	minecraft("com.mojang:minecraft:${project.minecraft_version}")
	mappings("org.wksh:mcp:${project.mcp_mappings}:v2")
	modImplementation("babric:fabric-loader:${project.loader_version}")

	implementation 'org.apache.logging.log4j:log4j-slf4j18-impl:2.17.2'
	implementation 'org.apache.commons:commons-lang3:3.0'
	implementation 'org.slf4j:slf4j-api:1.8.0-beta4'
	implementation 'com.google.code.gson:gson:2.8.9'
	implementation 'blue.endless:jankson:1.2.1'

	modImplementation 'com.github.zr0n1:multiproto:0.4.1'
	modImplementation 'net.modificationstation:StationAPI:2.0-alpha.2.1'
	modImplementation 'maven.modrinth:glass-config-api:2.0.2'
}

if (!gradle.startParameter.taskNames.contains('MinecraftClient')) {
	configurations.configureEach {
		resolutionStrategy {
			force 'org.lwjgl.lwjgl:lwjgl_util:2.9.3'
			force 'org.lwjgl.lwjgl:lwjgl:2.9.3'
			force 'org.lwjgl.lwjgl:lwjgl-platform:2.9.3'
		}
	}
}

processResources {
	filesMatching("fabric.mod.json") {
		expand(
				"version": project.version,
				"refMap": "${archivesBaseName}.refmap.json"
		)
	}
}

tasks.withType(JavaCompile).configureEach { it.options.release = 17 }
